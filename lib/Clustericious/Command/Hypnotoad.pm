=head1 NAME

Clustericious::Command::Hypnotoad

=head1 DESCRIPTION

Start a hypnotoad webserver.

Configuration for the server is taken directly from the
"hypnotoad" entry in the config file, and turned into
a config file for hypnotoad.  The location of the file
can be controllled by setting HYPNOTOAD_CONFIG.

=head1 EXAMPLE

 start_mode : "hypnotoad"
 hypnotoad:
    env :
        HYPNOTOAD_CONFIG : /tmp/miniondriver.hypnotoad.conf
    workers : 1
    listen : [ "http://*:3000" ]
    keep_alive_timeout : 50
    pid_file : /tmp/minionrelay.pid


=head1 SEE ALSO

Mojo::Server::Hypnotoad

=cut

package Clustericious::Command::Hypnotoad;
use Clustericious::Log;

use Clustericious::App;
use Clustericious::Config;
use Mojo::Server::Hypnotoad;
use Data::Dumper;
use Cwd qw/getcwd abs_path/;
use base 'Mojo::Command';

use strict;
use warnings;

__PACKAGE__->attr(description => "Start a hypnotad web server.\n");

__PACKAGE__->attr(usage => <<EOT);
Usage $0: hypnotoad
No options are available.  The 'hypnotoad' entry in the config file
is used for configuration.
EOT

sub run {
    my $self = shift;
    # args are ignored, since hypnotoad doesn't take command line arguments.

    my $conf = Clustericious::Config->new($ENV{MOJO_APP})->hypnotoad;
    my $filename = $ENV{HYPNOTOAD_CONFIG} || "hypnotoad.conf";
    my $conf_file = abs_path($filename) or LOGDIE "Cannot compute absolute path for $filename.  Set HYPNOTOAD_CONFIG?";
    my %conf = %$conf;
    my $conf_string = Data::Dumper->Dump([\%conf],["conf"]);
    DEBUG "Starting hypnotoad (executable: $ENV{MOJO_EXE}, config: $conf_file)";
    DEBUG "Config : $conf_string";
    open my $fp, ">", $conf_file or die "Cannot write to $conf_file";
    print $fp "## This file is autogenerated, any changes will be lost.\n";
    print $fp "## It was created when running $ENV{MOJO_EXE}\n";
    print $fp "## The contents reflect the 'hypnotoad' entry for that application.\n";
    print $fp "## To change the location of this file, set the HYPNOTOAD_CONFIG environment variable.\n";
    print $fp "\n\n$conf_string\n";
    system("hypnotoad","--config",$conf_file,$ENV{MOJO_EXE})==0 or die "error starting hypnotoad : $!";
}


1;

